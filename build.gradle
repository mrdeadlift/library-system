buildscript {
	dependencies {
		classpath 'org.postgresql:postgresql:42.7.2'
	}
}

plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.9.25'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.25'
	id 'org.springframework.boot' version '3.5.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.jooq.jooq-codegen-gradle' version '3.19.24'
	id 'org.flywaydb.flyway' version '11.2.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'com.h2database:h2'
	
	// jOOQ code generation dependencies
	jooqCodegen 'org.postgresql:postgresql'
}

kotlin {
	compilerOptions {
		freeCompilerArgs.addAll '-Xjsr305=strict'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

// Flyway configuration
flyway {
	url = 'jdbc:postgresql://localhost:5432/librarydb'
	user = 'myuser'
	password = 'library'
	locations = ['classpath:db/migration']
	driver = 'org.postgresql.Driver'
}

// jOOQ code generation configuration
jooq {
	configuration {
		jdbc {
			driver = 'org.postgresql.Driver'
			url = 'jdbc:postgresql://localhost:5432/librarydb'
			user = 'myuser'
			password = 'library'
		}
		generator {
			name = 'org.jooq.codegen.KotlinGenerator'
			database {
				name = 'org.jooq.meta.postgres.PostgresDatabase'
				inputSchema = 'public'
			}
			generate {
				kotlinSetterJvmNameAnnotationsOnIsPrefix = true
				kotlinNotNullPojoAttributes = true
				kotlinNotNullRecordAttributes = true
			}
			target {
				packageName = 'com.example.library.jooq'
				directory = 'src/main/kotlin'
			}
		}
	}
}
